/*
 * MIT License
 *
 * Copyright (c) 2017 Barracks Inc.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

buildscript {
    ext {
        springBootVersion = '1.3.3.RELEASE'
        gradleDockerVersion = '1.2'
        branch = (System.getenv("BRANCH_NAME") ?: "LOCAL")
        buildNumber = (System.getenv("BUILD_NUMBER") ?: "SNAPSHOT")
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "se.transmode.gradle:gradle-docker:${gradleDockerVersion}"
    }
}

plugins {
    id "net.linguica.maven-settings" version "0.5"
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: 'application'
apply plugin: 'docker'

group = projectGroup

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = "io.barracks.packageservice.Application"

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name 'barracks'
        url "https://dl.bintray.com/barracksiot/libs/"
    }
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile 'org.springframework.boot:spring-boot-starter-data-rest'
    compile 'org.springframework.boot:spring-boot-starter-aop'
    compile 'org.springframework.data:spring-data-mongodb'
    compile "com.google.guava:guava:19.0"
    compile 'io.barracks.libs:common-libs:0.0.5-1'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'com.jayway.jsonpath:json-path:2.2.0'
    testCompile "nl.jqno.equalsverifier:equalsverifier:2.0.2"
    testCompile "de.flapdoodle.embed:de.flapdoodle.embed.mongo:1.50.5"
    testCompile 'org.assertj:assertj-core:3.4.1'
}

jar {
    version = branch + '-' + buildNumber
}

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

jacoco {
    toolVersion = "0.7.7.201606060606"
}

test {
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacoco.exec")
    }
}

jacocoTestReport {
    executionData = fileTree(dir: "$buildDir/jacoco", include: '**/*.exec')
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

task copyJar(type: Copy, dependsOn: build) {
    from jar
    into "build/docker"
}

task dist(type: Docker, dependsOn: copyJar) {
    push = false
    tagVersion = jar.version
    dockerfile = file('Dockerfile')
}

task deployVersion(type: Docker, dependsOn: copyJar) {
    push = true
    tagVersion = jar.version
    dockerfile = file('Dockerfile')
}

task deployLatest(type: Docker, dependsOn: copyJar) {
    push = true
    tagVersion = branch
    dockerfile = file('Dockerfile')
}

task deploy {
    dependsOn deployVersion, deployLatest
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}